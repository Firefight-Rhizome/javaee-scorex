plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

def projectName = 'javaee-scorex'
def repoUrl = 'https://github.com/sink772/javaee-scorex'
def snapshotSuffix = rootProject.hasProperty('release') ? '' : '-SNAPSHOT'
version = VERSION + snapshotSuffix

def pomConfig = {
    licenses {
        license {
            name "GNU General Public License, version 2"
            url "https://opensource.org/licenses/gpl-2.0"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "sink772"
            name "Jaechang Namgoong"
            email "sink772@gmail.com"
        }
    }
    scm {
        url repoUrl
    }
}

publishing {
    repositories {
        maven {
            def releasesUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-releases"
            def snapshotsUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = rootProject.hasProperty('nexusUsername') ? "$nexusUsername" : ''
                password = rootProject.hasProperty('nexusPassword') ? "$nexusPassword" : ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = GROUP
            artifactId = projectName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', projectName)
                root.appendNode('description', 'scorex package for Java SCORE')
                root.appendNode('url', repoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = rootProject.hasProperty('bintrayUser') ? "$bintrayUser" : ''
    key = rootProject.hasProperty('bintrayKey') ? "$bintrayKey" : ''
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = projectName
        licenses = ['GPL-2.0']
        vcsUrl = 'https://github.com/sink772/javaee-scorex.git'
        version {
            name = project.version
            gpg {
                sign = true
            }
        }
    }
}
